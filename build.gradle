import org.ajoberstar.grgit.Grgit

import java.time.Instant

buildscript {
    ext.kotlin_version = '1.1.2-2'
    ext.spring_boot_version = '1.5.3.RELEASE'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply plugin: 'kotlin-spring'
apply plugin: "kotlin-jpa"


group 'ru.edustor'
version '0.9-SNAPSHOT'


repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

//noinspection GroovyAssignabilityCheck
jar {
    destinationDir = new File(buildDir, "/dist")
    archiveName = "edustor.jar"
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

ext {
    def grgit = Grgit.open()
    shortCommitId = grgit.head().getAbbreviatedId()
    commitId = grgit.head().id
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build.properties").withWriter { w ->
            def p = new Properties()
            p['edustor.build.name'] = "Edustor Core"
            p['edustor.build.version'] = "${project.version}-$shortCommitId".toString()
            p['edustor.build.date'] = Instant.now().epochSecond.toString()
            p['edustor.build.commitId'] = commitId
            p['edustor.build.repo'] = "https://github.com/edustor/core"
            p.store(w, null)
        }
    }
}

processResources.finalizedBy createProperties

dependencies {
    compile project(':edustor-commons:auth')
    compile project(':edustor-commons:version')
    compile project(':edustor-commons:rabbit')
    compile project(':edustor-commons:models')
    compile project(':edustor-commons:api')
    compile project(':edustor-commons:storage')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-freemarker"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-amqp"
    compile 'org.postgresql:postgresql:42.1.1'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.4'
    compile 'com.mashape.unirest:unirest-java:1.4.7'
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'io.reactivex:rxkotlin:0.40.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-core:2.1.0-RC.1'
    testCompile 'com.nhaarman:mockito-kotlin:0.6.1'
}